pkgname=uchroma
pkgver=1.9.90
pkgrel=1
pkgdesc="Userspace RGB control for Razer Chroma peripherals"
arch=("x86_64")
url="https://github.com/hyperbliss/uchroma"
license=("LGPL-3.0-only")
depends=(
  "python"
  "python-argcomplete"
  "python-coloraide"
  "python-colorlog"
  "python-dbus-fast"
  "python-evdev"
  "python-frozendict"
  "python-numpy"
  "python-pyudev"
  "python-ruamel-yaml"
  "python-traitlets"
  "python-wrapt"
)
optdepends=(
  "python-gobject: GTK frontend"
  "libadwaita: GTK frontend (Adwaita widgets)"
)
makedepends=(
  "rust"
  "maturin"
  "python-installer"
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/hyperbliss/uchroma/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=("SKIP")
install="$pkgname.install"

build() {
  cd "$pkgname-$pkgver"
  make build MATURIN=maturin
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  make install DESTDIR="$pkgdir"
}
